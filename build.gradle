// Plugins are currently not compatible to subprojects
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile(
                'junit:junit:4.11',
                'org.mockito:mockito-all:1.9.5'
        )
    }

    project.ext.JETTY_VERSION = "9.4.11.v20180605"

    group = "com.baqend"

    distTar.compression "GZIP"

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    javadoc {
        options.addBooleanOption('html5', true)
    }

    bintray {
        user = project.properties.get('bintray_user')
        key = project.properties.get('bintray_api_key')

        configurations = ['archives'] //When uploading configuration files
        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true //If version should be auto published after an upload
        pkg {
            repo = 'maven'
            userOrg = 'baqend' //An optional organization name when the repo belongs to one of the user's orgs
            name = project.name
            desc = 'A restfull layer ontop of jetty server'
            websiteUrl = 'https://github.com/Baqend/restful-jetty'
            issueTrackerUrl = 'https://github.com/Baqend/restful-jetty/issues'
            vcsUrl = 'git@github.com:Baqend/restful-jetty.git'
            licenses = ['MIT']
            labels = ['REST', 'jetty', 'server']
            publicDownloadNumbers = true
        }
    }
}

wrapper {
    gradleVersion = '4.9'
}

task assemble {
    dependsOn 'restful-jetty:assemble', 'restful-jetty-server:assemble'
}

release {
    preTagCommitMessage = "[ci skip] pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "[ci skip] new version commit:"
    buildTasks = ['assemble']
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

afterReleaseBuild.dependsOn 'restful-jetty:bintrayUpload', 'restful-jetty-server:bintrayUpload'



