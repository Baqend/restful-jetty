// Plugins are currently not compatible to subprojects
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library-distribution'
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile(
                'junit:junit:4.13',
                'org.mockito:mockito-all:1.10.19'
        )
    }

    project.ext.JETTY_VERSION = "9.4.24.v20191120"

    group = "com.baqend"

    distTar.compression "GZIP"

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    javadoc {
        //options.addBooleanOption('html5', true)
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/baqend/restful-jetty")
                credentials {
                    username = project.properties.get('github_user')
                    password = project.properties.get('github_password')
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

wrapper {
    gradleVersion = '4.10.2'
}

task assemble {
    dependsOn 'restful-jetty:assemble', 'restful-jetty-server:assemble'
}

release {
    preTagCommitMessage = "[ci skip] pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "[ci skip] new version commit:"
    buildTasks = ['assemble']
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

afterReleaseBuild.dependsOn 'restful-jetty:publish', 'restful-jetty-server:publish'



