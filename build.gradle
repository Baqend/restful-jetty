plugins {
    id 'com.jfrog.bintray' version "1.1"
    id 'net.researchgate.release' version '2.2.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'java-library-distribution'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile (
            'org.eclipse.jetty:jetty-server:9.3.2.v20150730',
            'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.3.2.v20150730',
            'org.eclipse.jetty:jetty-servlets:9.3.2.v20150730',
            'org.eclipse.jetty.http2:http2-server:9.3.2.v20150730',
            'org.eclipse.jetty:jetty-alpn-server:9.3.2.v20150730',
            'org.eclipse.jetty:jetty-client:9.3.2.v20150730',
            'org.eclipse.jetty:jetty-alpn-client:9.3.2.v20150730',
            'org.eclipse.jetty.http2:http2-http-client-transport:9.3.2.v20150730',
            'org.eclipse.jetty.websocket:javax-websocket-client-impl:9.3.2.v20150730'
    )
    testCompile (
            'junit:junit:4.11',
            'org.mockito:mockito-all:1.9.5'
    )
}

group = "com.baqend"

distTar.compression "GZIP"

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

release {
    preTagCommitMessage = "[ci skip] pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "[ci skip] new version commit:"
    buildTasks = ['assemble']
    git {
        requireBranch = 'master'
        pushToRemote = 'github'
        pushToCurrentBranch = false
    }
}

afterReleaseBuild.dependsOn bintrayUpload

bintray {
    user = project.properties.get('bintray_user')
    key = project.properties.get('bintray_api_key')

    configurations = ['archives'] //When uploading configuration files
    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        userOrg = 'baqend' //An optional organization name when the repo belongs to one of the user's orgs
        name = 'restful-jetty'
        desc = 'A restfull layer ontop of jetty server'
        websiteUrl = 'https://github.com/Baqend/restful-jetty'
        issueTrackerUrl = 'https://github.com/Baqend/restful-jetty/issues'
        vcsUrl = 'git@github.com:Baqend/restful-jetty.git'
        licenses = ['MIT']
        labels = ['REST', 'jetty', 'server']
        publicDownloadNumbers = true
    }
}

