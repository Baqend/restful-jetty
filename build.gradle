// Plugins are currently not compatible to subprojects
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library-distribution'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile(
                'junit:junit:4.13',
                'org.mockito:mockito-all:1.10.19'
        )
    }

    project.ext.JETTY_VERSION = "9.4.42.v20210604"

    group = "com.baqend"

    distTar.compression "GZIP"

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    javadoc {
        //options.addBooleanOption('html5', true)
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: findProperty('sonatype_user'), password: findProperty('sonatype_password'))
                }

                snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: findProperty('sonatype_user'), password: findProperty('sonatype_password'))
                }

                pom.project {
                    name 'Restful Jetty'
                    packaging 'jar'
                    description 'A powerful REST Layer for Jetty'
                    url 'https://github.com/Baqend/restful-jetty'

                    scm {
                        connection 'scm:git:https://github.com/Baqend/restful-jetty'
                        developerConnection 'scm:git:https://github.com/Baqend/restful-jetty'
                        url 'https://github.com/Baqend/restful-jetty'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/Baqend/restful-jetty/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'com.baqend'
                            name 'Baqend GmbH'
                            email 'info@baqend.com'
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "com.baqend.restful-jetty"
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    username = findProperty('sonatype_user')
    password = findProperty('sonatype_password')
}

wrapper {
    gradleVersion = '4.10.2'
}

task assemble {
    dependsOn 'restful-jetty:assemble', 'restful-jetty-server:assemble'
}

release {
    preTagCommitMessage = "[ci skip] pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "[ci skip] new version commit:"
    buildTasks = ['assemble']
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

afterReleaseBuild.dependsOn 'restful-jetty:uploadArchives', 'restful-jetty-server:uploadArchives'



